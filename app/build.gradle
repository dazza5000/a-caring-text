buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        jcenter()
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'realm-android'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'com.neenbedankt.android-apt'

Properties props = new Properties()
props.load(new FileInputStream(file(project.property("acaringtext.properties"))))

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.amicly.acaringtext"
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
        manifestPlaceholders = [fabricApiKey:props['fabric']]

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            // Run code coverage reports by default on debug builds.
            testCoverageEnabled = true
            ext.betaDistributionReleaseNotes="Release Notes for this build."
            ext.betaDistributionEmails="darran@whereisdarran.com"


        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
        lintConfig file('./qa-check/lint.xml')
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }

    configurations.all {
        resolutionStrategy.force 'com.android.support:support-annotations:23.3.0'
    }
}

ext {
    supportLibraryVersion = '23.3.0'
    espressoVersion = '2.2.1'
    runnerVersion = '0.4.1'
    guavaVersion = '19.0'

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$supportLibraryVersion"
    compile "com.android.support:design:$supportLibraryVersion"
    compile "com.google.guava:guava:$guavaVersion"
    compile 'com.android.support:cardview-v7:23.3.0'
    // Needed for Android Job from Evernote
    compile 'com.google.android.gms:play-services-gcm:9.0.0'

    // Dagger 2 and Compiler
    compile 'com.google.dagger:dagger:2.0.1'
    apt "com.google.dagger:dagger-compiler:2.0.1"

    // Needed for @Generated annotation (missing in Java <= 1.6; therefore, Android)
    compile 'javax.annotation:jsr250-api:1.0'

    testCompile 'org.mockito:mockito-core:1.10.19'

    androidTestCompile 'com.android.support:support-annotations:23.3.0'

    androidTestCompile "com.android.support.test.espresso:espresso-core:$espressoVersion"

    androidTestCompile "com.android.support.test:runner:$runnerVersion"
    androidTestCompile "com.android.support.test:rules:$runnerVersion"
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }

    // Add Android Job from Evernote
    compile 'com.evernote:android-job:1.0.6'
}

apply from: 'qa-check.gradle'